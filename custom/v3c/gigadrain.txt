actor GigaDrainWep : MegaBuster
{
Weapon.AmmoUse 1
Weapon.AmmoGive 56
Obituary "%o was drained dry by %k's giga drain."
Weapon.SlotNumber 4
Inventory.Pickupmessage "Power up! Giga Drain!"
weapon.ammotype "GigaDrainAmmo"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
inventory.icon "GIGDICO"
Scale 2.0
States
{
Spawn:
GIGD D 1
loop
Ready:
SNAK I 0 ACS_ExecuteAlways(998,0,2)
SNAK I 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
SNAK I 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
SNAK I 1 A_Raise
Loop
Fire:
SNAK I 0 A_JumpIfNoAmmo("NoAmmo")
SNAK I 0 A_PlaySoundEx("weapon/leafshield","Weapon")
SNAK J 1 A_FireCustomMissile("GigaDrainSpawner",0,1,8,0)
SNAK K 3
SNAK J 1 A_Refire
SNAK J 34
Goto Ready+1
NoAmmo:
SNAK I 1 ACS_Execute(979,0)
Goto Ready+1
}
}

Actor GigaDrainSpawner
{
PROJECTILE
+NOINTERACTION
+NOTARGETSWITCH
+RIPPER
DAMAGE (0)
HEIGHT 1
RADIUS 1
SPEED 40
STATES
{
SPAWN:
TNT1 A 1
TNT1 A 5
TNT1 A 1 A_SpawnItemEx("GigaDrain",random(-40,40),random(-30,30),random(0,32),-20,0,0,0)
stop
}
}

Actor GigaDrain
{
PROJECTILE
+NOTARGETSWITCH
DAMAGE (10)
Obituary "%o was drained dry by %k's giga drain."
HEIGHT 8
RADIUS 8
+FORCEXYBILLBOARD
DAMAGETYPE "GigaDrain"
SPEED 20
SCALE 2.0
STATES
{
SPAWN:
GIGD A 1
GIGD A 6
GIGD B 6
GIGD C 6
stop
XDeath:
TNT1 A 1 A_GiveToTarget("GigaRestore",1)
stop
}
}

actor GigaRestore : Health
{
inventory.amount 3
inventory.maxamount 100
}

actor GigaDrainAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 56
}