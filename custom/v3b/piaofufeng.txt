//Piaofu Feng - Slowly rise up with a shield under you. Then toss it downward on release

actor PiaofuFengWep : MegaBuster
{
Weapon.AmmoUse 1
Weapon.AmmoGive 56
Weapon.SlotNumber 7
Tag  "Piaofu Feng"
Obituary "%o was blown away by %k's Piaofu Feng."
Inventory.Pickupmessage "Power up! Piaofu Feng!"
weapon.ammotype "PiaofuFengAmmo"
inventory.icon "PFNGICO"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
Scale 2.0
States
{
Spawn:
PFNG A -1
stop
Ready:
BUBB D 0 ACS_ExecuteAlways(998,0,41)
BUBB D 1 A_WeaponReady
goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
BUBB D 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
BUBB D 1 A_Raise
Loop
Fire:
BUBB D 0 A_JumpIfNoAmmo("NoAmmo")
BUBB D 0 A_PlaySoundEx("weapon/piaofufeng","Weapon")
BUBB E 1 A_SpawnItemEx("PiaofuFengTemp",0,0,-16,0,0,0,0)
BUBB F 2 ThrustThingZ(0,20,0,0)
BUBB F 1 A_TakeInventory("PiaofuFengAmmo",2)
BUBB F 0 A_Refire
BUBB F 1 A_FireCustomMissile("WindStormStart",0,0,8,0)
BUBB F 10
goto Ready+1
Hold:
BUBB F 0 A_JumpIfNoAmmo("NoAmmo")
BUBB F 0 A_SpawnItemEx("PiaofuFengTemp",0,0,-16,0,0,0,0)
BUBB F 2 ThrustThingZ(0,20,0,0)
BUBB F 1 A_TakeInventory("PiaofuFengAmmo",2)
BUBB F 0 A_Refire
BUBB F 1 A_PlaySoundEx("weapon/mbuster","Weapon")
BUBB FED 3
BUBB F 1 A_FireCustomMissile("WindStormStart",0,0,8,0)
BUBB F 10
goto Ready+1
NoAmmo:
BUBB D 1 ACS_Execute(979,0)
Goto Ready+1
}
}

actor PiaofuFengAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 56
}

actor PiaofuFengTemp
{
PROJECTILE
+RIPPER
Damage (2)
damagetype "WindStorm"
Height 8
Obituary "%o was blown away by %k's Piaofu Feng."
Radius 16
Scale 2.5
Speed 25
States
{
Spawn:
WINS ABCD 2
stop
Death:
TNT1 A 1
stop
}
}