actor PulseFlag : Inventory
{
Inventory.Amount 1
Inventory.MaxAmount 1
}

actor PulseStopper
{
PROJECTILE
-NOINTERACTION //magical hax
+NOTARGETSWITCH
+DONTBLAST
Radius 14
Height 14
Scale 1.5
Speed 12
ReactionTime 65
+RIPPER
Damage (6)
Obituary "%o was stopped by %k's Pulse Stopper"
States
{
Spawn:
PLSR A 10
PLSR A 0 A_CountDown
PLSR A 1 A_JumpIfCloser(100,"Shield")
goto Spawn+1
Shield:
TNT1 A 0 A_GiveToTarget("PulseFlag",1)
TNT1 A 1 A_ChangeFlag("NOINTERACTION",1)
TNT1 A 1 A_SpawnItemEx("PulseShield")
TNT1 A 1 A_GiveToTarget("Repulsion",1)
TNT1 A 0 A_ChangeFlag("NOINTERACTION",0)
TNT1 A 0 A_TakeFromTarget("PulseFlag",1)
goto Spawn+1
}
}

actor PulseShield //A_SetScale is broken online so...
{
Scale 5.0
+NOINTERACTION
+NOGRAVITY
States
{
Spawn:
PLSR A 0
PLSR A 4
stop
}
}

actor PulseStopperWep : MegaBuster
{
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Tag "Pulse Stopper"
Inventory.Pickupmessage "Power Up! Pulse Stopper."
Obituary "%o was stopped by %k's Pulse Stopper"
weapon.ammotype "PulseStopperAmmo"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
inventory.icon "PLSRICO"
Scale 2.0
States
{
Spawn:
PLSR B 1
loop
Ready:
AIRS D 0 ACS_ExecuteAlways(998,0,38)
AIRS D 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
AIRS D 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
AIRS D 1 A_Raise
Loop
Fire:
AIRS D 0 A_JumpIfNoAmmo("NoAmmo")
AIRS D 0 A_PlaySoundEx("weapon/pulsestopper","Weapon")
AIRS D 0 A_FireCustomMissile("PulseStopper",0,1,8,0)
AIRS EFD 3
AIRS D 19
AIRS D 0 A_Refire
Goto Ready+1
NoAmmo:
AIRS D 1 ACS_Execute(979,0)
Goto Ready+1
}
}

actor PulseStopperAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
}