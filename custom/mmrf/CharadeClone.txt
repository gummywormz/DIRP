//needs more "use skin of player" commands D:

//Thing_ChangeTID(0,500+ACS_ExecuteWithResult(257, 0))

actor CharadeFlag : Inventory
{
Inventory.amount 1
Inventory.maxamount 1
}


actor CharadePainFlag : Inventory
{
Inventory.amount 1
Inventory.maxamount 1
}

actor CharadeClone
{
PROJECTILE
+NOINTERACTION
+FIXMAPTHINGPOS
+FLOORHUGGER
Height 1
Scale 2.5
Speed 0
Radius 1
States
{
Spawn:
COPY C 0
COPY C 1 Thing_ChangeTID(0,500+ACS_ExecuteWithResult(257, 0))
goto AfterTID
AfterTID:
COPY C 1 A_JumpIfInTargetInventory("IsDead",1,"Death")
loop
Death:
TNT1 A 0
stop
}
}

Actor CharadeCloneProj
{
PROJECTILE
Height 8
Radius 15
Scale 2.5
+DONTBLAST
Speed 0
Obituary "%o was surprised by %k's Charade Clone"
Damage (1)
States
{
Spawn:
COPY C -1
stop
Death:
COPY C 1 A_PlaysoundEx("weapon/drillexplode","Voice")
COPY C 1 A_Explode(65,192,0)
COPY KJIHGF 8
stop
}
}

actor CharadeCloneWep : MegaBuster
{
Weapon.AmmoUse 14
Weapon.AmmoGive 56
Weapon.SlotNumber 7
Tag "Charade Clone"
Inventory.Pickupmessage "Power Up! Charade Clone."
Obituary "%o was surprised by %k's Charade Clone"
weapon.ammotype "CharadeCloneAmmo"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
inventory.icon "CHRDICO"
Scale 2.0
States
{
Spawn:
CHRD A -1
stop
Ready:
AIRS D 0 ACS_ExecuteAlways(998,0,38)
AIRS D 0 A_TakeInventory("CharadePainFlag",1)
AIRS D 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
AIRS D 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
AIRS D 1 A_Raise
Loop
Fire:
AIRS D 1 A_JumpifInventory("CharadeFlag",1,"ShootIt")
AIRS D 0 A_JumpIfNoAmmo("NoAmmo")
AIRS D 0 A_TakeInventory("CharadeCloneAmmo",14)
AIRS D 0 A_PlaySoundEx("weapon/charadeclone","Weapon")
AIRS D 1 A_SetTranslucent (0.0, 0)
AIRS D 1 A_SpawnItemEx("CharadeClone")
AIRS D 0 A_GiveInventory("CharadeFlag",1)
AIRS D 8
goto CloneOut
CloneOut:
AIRS D 0 A_JumpifInventory("CharadePainFlag",1,"Caught")
AIRS D 1 A_WeaponReady(WRF_NOSWITCH)
loop
Caught:
AIRS D 0 A_TakeInventory("CharadePainFlag",1)
AIRS D 0 A_TakeInventory("CharadeFlag",1)
AIRS D 0 A_PlaySoundEx("weapon/magdrillexplode","Weapon")
AIRS D 1 A_SetTranslucent (1.0, 0)
AIRS D 1 Thing_Move(tid,tid+500,0)
AIRS D 0 SetPlayerProperty(0,1,4)
AIRS D 0 Thing_Remove(500+ACS_ExecuteWithResult(257, 0))
AIRS D 17
AIRS D 0 SetPlayerProperty(0,0,4)
goto Ready+1
ShootIt:
AIRS D 1 A_TakeInventory("CharadeFlag",1)
AIRS D 0 A_TakeInventory("CharadePainFlag",1)
AIRS D 0 A_PlaySoundEx("item/refill","Weapon")
AIRS D 1 A_SetTranslucent (1.0, 0)
AIRS D 1 A_SpawnItemEx("CharadeCloneProj",0,0,198,0,0,-32)
AIRS D 1 Thing_Remove(500+ACS_ExecuteWithResult(257, 0))
AIRS D 18
goto Ready+1
NoAmmo:
AIRS D 1 ACS_Execute(979,0)
Goto Ready+1
}
}

actor CharadeCloneAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 56
}