//Shoot a rock that goes in a straight line but slowly. Upon Impact with a wall
//it will shatter into a bunch of pieces.

actor ShatterStone1
{
PROJECTILE
Damage (40)
damagetype "SuperArm"
Height 20
Radius 20
Speed 10
Obituary "%o was broken by %k's Shatter Stone."
scale 2.5
States
{
Spawn:
SARM A 0
SARM A 5
SARM A 1 A_ChangeVelocity(20, 0, 0, CVF_RELATIVE | CVF_REPLACE)
SARM A 8
SARM A 1 A_ChangeVelocity(30, 0, 0, CVF_RELATIVE | CVF_REPLACE)
SARM A 10
SARM A 1 A_ChangeVelocity(35, 0, 0, CVF_RELATIVE | CVF_REPLACE)
Goto DoneScaling
DoneScaling:
SARM A 1
loop
Death:
SARM A 0 A_SpawnItemEx("ShatterDebris", -32, random(10,50), 32, 30, 0, 10, 0, 0)
SARM A 0 A_SpawnItemEx("ShatterDebris", -32, random(10,50), 32, 30, 0, 10, 0, 0)
SARM A 0 A_SpawnItemEx("ShatterDebris", -32, random(10,50), 32, 30, 0, 10, 0, 0)
SARM A 0 A_SpawnItemEx("ShatterDebris", -32, random(10,50), -32, 30, 0, 10, 0, 0)
SARM A 0 A_SpawnItemEx("ShatterDebris", -32, random(10,50), -32, 30, 0, 10, 0, 0)
SARM A 0 A_SpawnItemEx("ShatterDebris", -32, random(10,50), -32, 30, 0, 10, 0, 0)
TNT1 A 2 A_PlaySoundEx("weapon/napalm", "Voice")
stop
XDeath:
TNT1 A 1
stop
}
}


actor ShatterDebris : GutrockDebris
{
-NOGRAVITY
Obituary "%o was broken by %k's Shatter Stone."
Damage (10)
Speed 31
States
{
Spawn:
SARM B 0
SARM B 1 A_ScaleVelocity(-1.0)
SARM B 1
SARM B 1
SARM B 1
goto Spawn+3
}
}

actor ShatterStoneWep : MegaBuster
{
Weapon.AmmoUse 5
Weapon.AmmoGive 30
Weapon.SlotNumber 5
Inventory.Pickupmessage "Power Up! Shatter Stone!"
Obituary "%o was broken by %k's Shatter Stone."
Weapon.AmmoType "ShatterStoneAmmo"
Inventory.Icon "SHSTICO"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
Scale 2.0
States
{
Spawn:
SHST A -1
stop
Ready:
PINM A 0 ACS_ExecuteAlways(998,0,14)
PINM A 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
PINM A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
PINM A 1 A_Raise
Loop
Fire:
PINM A 1 A_JumpIfNoAmmo("NoAmmo")
PINM A 0 A_PlaySoundEx("weapon/powerstone","Weapon")
PINM A 1 A_FireCustomMissile("ShatterStone1",0,1,8,0)
PINM BCD 5
PINM A 15
PINM A 1 A_Refire
Goto Ready+1
NoAmmo:
PINM A 1
Goto Ready+1
}
}

actor ShatterStoneAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 30
}