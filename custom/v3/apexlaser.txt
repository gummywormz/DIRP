//after 3 second warning, send 2 large instakill lasers down sides

actor ApexGiver : CustomInventory
{
Inventory.Pickupmessage "Power up! Apex Laser!"
inventory.pickupsound "weapon/weaponup"
Inventory.RespawnTics 1800
scale 2.0
states
{
Spawn:
APXL F -1
stop
Pickup:
APXL F 0 A_JumpIfInventory("ApexLaserWep",1,"Fale")
APXL F 0 A_GiveInventory("ApexLaserWep")
stop
Fale:
APXL F 0
fail
}
}

actor ApexAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 1
}

Actor ApexLaserWep : BaseMM8BDMWep
{
Weapon.AmmoUse 1
Weapon.AmmoGive 1
Weapon.SlotNumber 5
+WEAPON.NOAUTOAIM
Tag  "Apex Laser"
inventory.icon "APXLICO"
Obituary "%o was vaporized by %k's Apex Laser."
weapon.ammotype "ApexAmmo"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
Scale 2.0
States
{
Spawn:
TNT1 A 1
stop
Ready:
APXL A 0 ACS_ExecuteAlways(998,0,330)
APXL A 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
APXL A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
APXL A 1 A_Raise
Loop
Fire:
APXL A 0 A_JumpIfNoAmmo("NoAmmo")
APXL BC 2
APXL D 0 SetPlayerProperty(0,1,4)
APXL D 17 A_PlaySoundEx("weapon/apexwarn","Weapon")
APXL D 1
APXL D 17
APXL D 2 A_JumpIfInventory("IsDead",1,"NoAmmo")
APXL D 0
APXL D 2 A_PlaySoundEx("weapon/laserrocks","Voice")
APXL D 0 A_FireCustomMissile("ApexLaserSpawner",0,0,8,0)
APXL D 0 A_FireCustomMissile("ApexLaserSpawner",0,1,-64,0)
APXL D 35
APXL CBA 4
APXL A 1 SetPlayerProperty(0,0,4)
Goto NoAmmo
NoAmmo:
APXL B 1 ACS_Execute(979,0)
APXL B 1 A_TakeInventory("ApexLaserWep",1)
stop
}
}

actor ApexLaserSpawner
{
PROJECTILE
+LOOKALLAROUND
+SKYEXPLODE
+DONTBLAST
+RIPPER
Obituary "%o was vaporized by %k's Apex Laser."
damagetype "GeminiLaser"
Speed 0
Damage (0)
radius 80
height 40
scale 5.0
reactiontime 22
States
{
Spawn:
TNT1 A 1
TNT1 A 1 A_SpawnItemEx("ApexLaser",0,0,0,50,0,0,0)
TNT1 A 3 A_Countdown
loop
Death:
TNT1 A 0
stop
}
}

actor ApexLaser
{
PROJECTILE
+LOOKALLAROUND
+SKYEXPLODE
+DONTBLAST
+RIPPER
Obituary "%o was vaporized by %k's Apex Laser."
damagetype "GeminiLaser"
Speed 50
Damage (50)
radius 5
height 5
scale 5.0
States
{
Spawn:
APXL E -1
stop
Death:
TNT1 A 0
stop
}
}