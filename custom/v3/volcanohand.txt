//rapid fire flamethrower, low distance low damage, and remain still when using.

actor VolcanoHandWep : MegaBuster
{
Weapon.AmmoUse 2
Weapon.AmmoGive 50
Weapon.SlotNumber 4
scale 2.0
Tag  "Volcano Hand"
inventory.icon "VOLHICO"
Obituary "%o was cooked up by %k's Volcano Hand."
Inventory.Pickupmessage "Power up! Volcano Hand"
inventory.pickupsound "weapon/weaponup"
weapon.ammotype "VolcanoHandAmmo"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
States
{
Spawn:
VOLH A -1
stop
Ready:
TKSH A 0 ACS_ExecuteAlways(998,0,21)
TKSH A 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
TKSH A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
TKSH A 1 A_Raise
Loop
Fire:
TKSH A 0 A_JumpIfNoAmmo("NoAmmo")
TKSH A 0 SetPlayerProperty(0,1,0)
TKSH A 0 A_PlaySoundEx("weapon/firestorm","Weapon")
TKSH A 0 A_FireCustomMissile("HandShot",random(-9,9),1,8,0)
TKSH IJK 1
TKSH K 1
TKSH K 0 A_Refire
TKSH A 0 SetPlayerProperty(0,0,0)
TKSH JI 1
goto Ready+1
Hold:
TKSH A 0 A_JumpIfNoAmmo("NoAmmo")
TKSH K 0 SetPlayerProperty(0,1,0)
TKSH K 0 A_PlaySoundEx("weapon/firestorm","Weapon")
TKSH K 0 A_FireCustomMissile("HandShot",random(-9,9),1,8,0)
TKSH K 1
TKSH K 1
TKSH K 0 A_Refire
TKSH K 0 SetPlayerProperty(0,0,0)
goto Ready+1
NoAmmo:
TKSH A 0 SetPlayerProperty(0,0,0)
TKSH A 1 ACS_Execute(979,0)
Goto Ready+1
}
}

actor HandShot : FastProjectile
{
PROJECTILE
+LOOKALLAROUND
+SKYEXPLODE
+DONTBLAST
Obituary "%o was cooked up by %k's Volcano Hand."
damagetype "FireStorm"
Speed 70
Damage (5)
radius 6
height 7
scale 2.5
States
{
Spawn:
TNT1 A 1
FIRE ABC 1
stop
Death:
FIRE A 1
stop
}
}

actor VolcanoHandAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 50
}