//boomerang but holds for a while. charge for high speed bouncing projectile
actor SpikeBall1
{
PROJECTILE
+RIPPER
+NOTARGETSWITCH
Height 12
Radius 12
DamageType "RingBoomerang"
Scale 1.5
Damage (6)
Speed 40
ReactionTime 20
Obituary "%o was prodded by %k's Spike Ball."
States
{
Spawn:
SKBL A 1
SKBL ABCD 2
SKBL A 0 A_Stop
SKBL ABCD 2
SKBL ABCD 2
SKBL ABCD 2
goto Death
Death:
TNT1 A 1 A_CustomMissile("SpikeBallReturn",0,0,0,0)
stop
XDeath:
TNT1 A 1
stop
}
}

actor SpikeBall2
{
+RIPPER
+HEXENBOUNCE
BounceType HEXENCOMPAT
WallBounceFactor 0.8
BounceFactor 0.8
Obituary "%o was prodded by %k's Spike Ball."
PROJECTILE
Damage (9)
Height 12
DamageType "RingBoomerang"
Radius 12
Scale 1.5
gravity 3.0
speed 45
BounceCount 4
States
{
Spawn:
SKBL EFGH 2
loop
}
}

actor SpikeBallReturn : RingBoomerang
{
+NOINTERACTION
Scale 1.5
Obituary "%o was prodded by %k's Spike Ball."
damagetype "RingBoomerang"
radius 0
height 0
States
{
Spawn:
SKBL A 0
SKBL A 0 A_JumpIfCloser(32,"Death")
SKBL AB 1 A_SpawnItemEx("SpikeBallDamager",0,0,0,2,0,0)
SKBL B 0 A_JumpIfCloser(32,"Death")
SKBL CD 1 A_SpawnItemEx("SpikeBallDamager",0,0,0,2,0,0)
SKBL A 0 A_JumpIfCloser(32,"Death")
SKBL AB 1 A_SpawnItemEx("SpikeBallDamager",0,0,0,2,0,0)
SKBL C 0 A_JumpIfCloser(32,"Death")
SKBL CD 1 A_SpawnItemEx("SpikeBallDamager",0,0,0,2,0,0)
SKBL D 0 A_Stop
stop
Death:
SKBL D 0
stop
}
}

actor SpikeBallDamager
{
damagetype "RingBoomerang"
PROJECTILE
Obituary "%o was prodded by %k's Spike Ball."
+RIPPER
damage (6)
radius 12
height 12
States
{
Spawn:
TNT1 A 2
stop
}
}

actor SpikeBallWep : Megabuster
{
Weapon.AmmoUse 2
Weapon.AmmoGive 56
Weapon.SlotNumber 2
Inventory.Icon "SKBLICO"
scale 2.0
inventory.pickupsound "weapon/weaponup"
Obituary "%o was prodded by %k's Spike Ball."
Inventory.Pickupmessage "Power Up! Spike Ball!"
weapon.ammotype "SpikeBallAmmo"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
States
{
Spawn:
SKBL I -1
stop
Ready:
SKBL J 0 ACS_ExecuteAlways(998,0,234)
SKBL J 0 A_TakeInventory("SpikeBallCharge",999)
SKBL J 1 A_WeaponReady
Goto Ready+2
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
SKBL J 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
SKBL J 1 A_Raise
Loop
Fire:
SKBL J 0 A_JumpIfNoAmmo("NoAmmo")
SKBL J 0 A_PlaySoundEx("weapon/superboost","Weapon")
SKBL J 0 A_FireCustomMissile("SpikeBall1",0,1,8,0)
SKBL LKJ 2 A_TakeInventory("SpikeBallCharge",999)
SKBL J 7
SKBL J 1 A_Refire
goto Ready+2
Fire2:
SKBL J 0 A_TakeInventory("SpikeBallCharge",999)
SKBL J 3
SKBL LKJ 3
SKBL J 0 A_Refire
goto Ready+2
Fire3:
SKBL J 0
SKBL J 0 A_TakeInventory("SpikeBallCharge",999)
SKBL J 0 A_PlaySoundEx("weapon/coilbounce2","Weapon")
SKBL J 0 A_FireCustomMissile("SpikeBall2",0,1,8,0)
SKBL J 0 A_TakeInventory("SpikeBallAmmo",2)
SKBL LK 3
SKBL J 20
SKBL J 0 A_Refire
goto Ready+1
Hold:
SKBL J 0 A_JumpIfInventory("SpikeBallCharge",33,"BotCheck")
SKBL J 0 A_JumpIfInventory("SpikeBallCharge",32,"Charge2")
SKBL J 0 A_JumpIfInventory("SpikeBallCharge",18,"Charge")
SKBL K 1 A_GiveInventory("SpikeBallCharge",1)
SKBL J 0 A_Refire
Goto Ready+1
BotCheck:
SKBL J 0 ACS_ExecuteAlways(974,0)
Goto Fire3
Charge:
SKBL J 0 A_JumpIfInventory("SpikeBallCharge",33,"BotCheck")
SKBL J 0 A_JumpIfInventory("SpikeBallCharge",32,"Charge2")
SKBL J 0 A_JumpIfInventory("SpikeBallCharge",19,"Charge1")
SKBL K 1
SKBL J 0 A_PlaySoundEx("weapon/coilstart","Weapon")
Goto Charge1
Charge1:
SKBL K 0 A_JumpIfInventory("SpikeBallCharge",33,"BotCheck")
SKBL K 0 A_JumpIfInventory("SpikeBallCharge",32,"Charge2")
SKBL K 1 A_GiveInventory("SpikeBallCharge",1)
SKBL O 2
SKBL O 0 A_Refire
Goto Fire2
Charge2:
SKBL J 0 A_PlaySoundEx("weapon/coilcharge","Weapon")
SKBL J 0
SKBL M 0
SKBL M 1
SKBL N 1
SKBL O 2
SKBL J 0 A_Refire
Goto Fire3
NoAmmo:
SKBL J 1 ACS_Execute(979,0)
Goto Ready+1
}
}

actor SpikeBallCharge : Inventory
{
inventory.amount 1
inventory.maxamount 52
}

actor SpikeBallAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 56
}