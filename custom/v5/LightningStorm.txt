//gains ammo with movement.

actor LightningStormAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
}

actor StormCharge : Inventory
{
inventory.amount 1
inventory.maxamount 208
}

actor StormCheck : custominventory
{
States
{
Pickup:
TNT1 A 1 
TNT1 A 0 A_JumpIf(ACS_ExecuteWithResult(608) == 1,"Charge")
stop
Charge:
TNT1 A 0 A_GiveInventory("StormCharge",1)
stop
}
}

actor PlasmaArrow
{
PROJECTILE
+RIPPER
Damage (5)
Speed 45
scale 2.5
Radius 6
Height 7
DamageType "Plasma"
Obituary "%o was shocked by %k's lightning arrow."
States
{
Spawn:
ARRO C -1
stop
}
}

actor PlasmaBlast
{
PROJECTILE
Damage (15)
Speed 40
scale 2.5
radius 20
height 20
DamageType "Plasma"
Obituary "%o was shocked by %k's lightning blast."
States
{
Spawn:
ARRO B -1
stop
}
}

actor PlasmaLaser
{
PROJECTILE
Damage (40)
Speed 30
scale 3.0
radius 20
height 20
DamageType "Plasma"
Obituary "%o was shocked by %k's lightning laser."
States
{
Spawn:
ARRO A -1
stop
}
}

actor LightningStormWep : BaseMM8BDMWep
{
//$Category MM8BDM-Weapons
//$Title Thunder Bolt
Weapon.AmmoUse 4
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Inventory.Pickupmessage "Power Up! Lightning Storm!"
Obituary "%o was shocked by %k's lightning arrow."
Tag "Lightning Storm"
weapon.ammotype "LightningStormAmmo"
inventory.icon "TBOLSI"//do
States
{
Spawn:
PLSM A 1 //do
loop
Ready:
TBOL S 0 ACS_ExecuteAlways(998,0,56)
TBOL S 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
TBOL S 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
TBOL S 1 A_Raise
Loop
Fire:
TBOL S 0 A_JumpIfNoAmmo("NoAmmo")
TBOL S 0 A_JumpIfInventory("StormCharge",200,"Laser")
TBOL S 0 A_JumpIfInventory("StormCharge",100,"Blast")
TBOL S 0 A_JumpIfInventory("StormCharge",50,"Arrow")
goto NoAmmo
Laser:
TBOL S 0 A_PlaySoundEx("weapon/pshot","Weapon")
TBOL S 0 A_FireCustomMissile("PlasmaLaser",0,1,8,0)
TBOL S 0 A_TakeInventory("StormCharge",999)
TBOL TU 5
TBOL S 14
TBOL S 0 A_Refire
Goto Ready+1
Blast:
TBOL S 0 A_PlaySoundEx("weapon/thunderbolt","Weapon")
TBOL S 0 A_FireCustomMissile("PlasmaBlast",0,1,8,0)
TBOL S 0 A_TakeInventory("StormCharge",999)
TBOL TU 5
TBOL S 7
TBOL S 0 A_Refire
Goto Ready+1
Arrow:
TBOL S 0 A_PlaySoundEx("weapon/mbuster","Weapon")
TBOL S 0 A_FireCustomMissile("PlasmaArrow",0,1,8,0)
TBOL S 0 A_TakeInventory("StormCharge",999)
TBOL TU 5
TBOL S 3
TBOL S 0 A_Refire
Goto Ready+1
NoAmmo:
TBOL S 1 ACS_Execute(979,0)
Goto Ready+1
}
}
