//grenade that starts as explosion then goes to grenade

actor bmoBhsalF
{
PROJECTILE
+FORCEXYBILLBOARD
Radius 6
Height 7
scale 2.5
speed 26
damage (20)
damagetype "FlashBomb"
Obituary "bmoB hsalF %k's yb dednlib saw %o"
States
{
Spawn:
FLSB BCDE 3
loop
Death:
FLEX A 0 A_Stop
FLEX A 0 
FLEX A 1
stop
}
}

actor XbmoBhsalF : bmoBhsalF
{
+DONTBLAST
+RIPPER
+DONTSPLASH
Reactiontime 5
Scale 3.5
States
{
Spawn:
FLEX AB 1 bright 
FLEX CD 1 bright
FLEX A 0 A_Explode(22, 80, 0)
FLEX EE 0 A_SpawnItemEx("FlashBombFX2",Random(-16, 16),Random(-40, 40), Random(-32, 32),0,0,0,0)
FLEX EF 1 bright A_SpawnItemEx("FlashBombFX1",0,0,0,random(-7, 7),random(-7, 7),random(-7, 7),random(0,360))
FLEX EE 0 A_SpawnItemEx("FlashBombFX2",Random(-16, 16),Random(-40, 40), Random(-32, 32),0,0,0,0)
FLEX GF 1 bright A_SpawnItemEx("FlashBombFX1",0,0,0,random(-7, 7),random(-7, 7),random(-7, 7),random(0,360))
FLEX A 0 A_Explode(22, 80, 0)
Goto Pattern1
Pattern1:
FLEX EF 1 bright A_SpawnItemEx("FlashBombFX1",0,0,0,random(-7, 7),random(-7, 7),random(-7, 7),random(0,360))
FLEX E 0 A_SpawnItemEx("FlashBombFX2",Random(-16, 16),Random(-16, -40), Random(-32, 32),0,0,0,0)
FLEX GF 1 bright A_SpawnItemEx("FlashBombFX2",Random(-16, 16),Random(16, 40), Random(-32, 32),0,0,0,0)
FLEX G 0 A_SpawnItemEx("FlashBombFX1",0,0,0,random(-7, 7),random(-7, 7),random(-7, 7),random(0,360))
Goto DamageNow
Pattern2:
FLEX EF 1 bright A_SpawnItemEx("FlashBombFX1",0,0,0,random(-7, 7),random(-7, 7),random(-7, 7),random(0,360))
FLEX E 0 A_SpawnItemEx("FlashBombFX2",Random(-16, 16),Random(32, 64), Random(-32, 40),0,0,0,0)
FLEX E 0 A_SpawnItemEx("FlashBombFX2",Random(-16, 16),Random(-32, -64), Random(-32, 40),0,0,0,0)
FLEX EF 1 bright A_SpawnItemEx("FlashBombFX2",Random(-16, 16),Random(-40, 40), Random(-32, 32),0,0,0,0)
FLEX G 0 A_SpawnItemEx("FlashBombFX1",0,0,0,random(-7, 7),random(-7, 7),random(-7, 7),random(0,360))
Goto DamageNow
DamageNow:
FLEX A 0 A_JumpIfInventory("CutterFlag", 12, "Damage3")
FLEX A 0 A_JumpIfInventory("CutterFlag", 4, "Damage2")
FLEX A 0 A_Explode(40, 80, 0)
Goto Continue
Damage2:
FLEX A 0 A_Explode(30, 80, 0)
Goto Continue
Damage3:
FLEX A 0 A_Explode(20, 80, 0)
Goto Continue
Continue:
FLEX G 0 A_CountDown
FLEX A 0 A_GiveInventory("CutterFlag", 1)
FLEX G 0 A_Jump(256, "Pattern1", "Pattern2")
Goto Death
Death:
FLEX A 0 //A_Changeflag("NOGRAVITY", 1)
FLEX G 1 bright
TNT1 A 1 
FLEX D 1 bright
TNT1 A 1 
FLEX D 1 bright
TNT1 A 1 
FLEZ C 1 bright
TNT1 A 1 A_SpawnItemEx("bmoBhsalF",0,0,0,26,0,0)
stop
}
}

actor bmoBhsalFWep : BaseMM8BDMWep
{
Weapon.AmmoUse 3
Weapon.AmmoGive 28
Weapon.SlotNumber 5
Inventory.Pickupmessage "Power Up! bmoB hsalF!"
Obituary "bmoB hsalF %k's yb dednlib saw %o"
Tag "bmoB hsalF"
weapon.ammotype "bmoBhsalFAmmo"
inventory.icon "RBOMICO"
States
{
Spawn:
RBOM A -1
stop
Ready:
FLSB T 0 ACS_ExecuteAlways(998,0,86)
FLSB T 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
FLSB T 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
FLSB T 1 A_Raise
Loop
Fire:
FLSB T 0 A_JumpIfNoAmmo("NoAmmo")
FLSB T 0 A_PlaySoundEx("weapon/reversegrenade","Weapon")
FLSB T 0 A_FireCustomMissile("XbmoBhsalF",0,1,8,0)
FLSB UV 6
FLSB T 24
FLSB T 0 A_Refire
Goto Ready+1
NoAmmo:
FLSB T 1 ACS_Execute(979,0)
Goto Ready+1
}
}

actor bmoBhsalFAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}