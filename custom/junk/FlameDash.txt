//thrust player and leave trail of flames. damage self when crash into a wall

//icon
Actor FlameDashWep : MegaBuster
{
Weapon.AmmoUse 25
Weapon.AmmoGive 50
Weapon.SlotNumber 5
+WEAPON.NOAUTOAIM
inventory.icon "FDSHICO"
Obituary "%o was burnt to a char by %k's Flame Dash."
weapon.ammotype "FlameDashAmmo"
inventory.pickupsound "weapon/weaponup"
+WEAPON.AMMO_OPTIONAL
Scale 2.0
States
{
Spawn:
FDSH A 1
loop
Ready:
FHUD A 0 ACS_ExecuteAlways(998,0,81)
FHUD A 1 A_WeaponReady
Goto Ready+1
Deselect:
FHUD A 1 A_TakeInventory("StopMeNow",9)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
FHUD A 1 A_Lower
Loop
Select:
FHUD A 1 A_TakeInventory("StopMeNow",9)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
FHUD A 1 A_Raise
Loop
Fire:
FHUD A 0 A_JumpIfNoAmmo("NoAmmo")
FHUD C 2
FHUD D 0 SetPlayerProperty(0,1,4)
FHUD DEFG 4
TNT1 A 17
TNT1 A 1 A_TakeInventory("FlameDashAmmo",25)
TNT1 A 1 A_PlaySoundEx("weapon/flamedash","Weapon")
TNT1 A 1 ThrustThing(angle*256/360, 80, 1, 0)
TNT1 A 0
TNT1 A 0
TNT1 A 0
TNT1 A 0 A_JumpIfInventory("StopMeNow",1,"StopMe")
TNT1 A 0 A_JumpIf(momx == 0,"StopMe")
TNT1 A 0
TNT1 A 1 A_FireCustomMissile("FlameDashRammer",0,0,8,0)
TNT1 A 1 A_SpawnItemEx("FlameDashTrail")
TNT1 A 3
goto Fire+8
StopMe:
FHUD HI 2
FHUD A 10
FHUD A 1 SetPlayerProperty(0,0,4)
FHUD A 1 A_TakeInventory("StopMeNow",9)
FHUD A 35
Goto Ready+1
NoAmmo:
FHUD B 1 ACS_Execute(979,0)
goto Ready+1
}
}

actor FlameDashRammer
{
PROJECTILE
+DONTSPLASH
+NOTARGETSWITCH
Damagetype "QuickBoomerang"
Damage (3)
States
{
Spawn:
TNT1 A 0
TNT1 A 2
stop
XDeath:
TNT1 A 1 
stop
Death:
TNT1 A 2 A_GiveToTarget("StopMeNow",2)
TNT1 A 1 A_Explode(20,128,1)
stop
}
}

actor FlameDashTrail
{
PROJECTILE
+DONTSPLASH
+NOTARGETSWITCH
Damagetype "FireStorm"
Damage (3)
+RIPPER
States
{
Spawn:
TNT1 A 0
FWVE BA 3
stop
}
}

actor StopMeNow : Inventory
{
inventory.amount 1
inventory.maxamount 1
}

actor FlameDashAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 50
}