actor TopRider
{
PROJECTILE
Speed 0
Damage (1)
damagetype "TopSpin"
Obituary "$%o was spun right round by $k's Top Rider"
+RIPPER
-NOGRAVITY
+FLOORHUGGER
ReactionTime 20
Radius 48
Height 2
scale 2.5
States
{
Spawn:
ROPP AABBCC 1 A_JumpIfCloser(16,"TimeToRide")
ROPP A 1 A_CountDown
loop
TimeToRide:
TNT1 A 0 A_GiveToTarget("TopRiderFlag",1)
goto Death
Death:
MMFX BCDE 2
stop
}
}

actor TopRiderFlag : Inventory
{
Inventory.Amount 1
Inventory.MaxAmount 1
}

actor TopRiderWep : BaseMM8BDMWep
{
Weapon.AmmoUse 4
Weapon.AmmoGive 56
Weapon.SlotNumber 4
Inventory.Pickupmessage "Power Up! Top Rider"
Obituary "$%o was spun right round by $k's Top Rider"
Tag "Top Rider"
weapon.ammotype "TopRiderAmmo"
inventory.icon "ROPPICO"
States
{
Spawn:
ROPP D -1
stop
Ready:
THUN H 0 ACS_ExecuteAlways(998,0,9)
THUN H 1 A_WeaponReady
THUN H 0 A_JumpIfInventory("TopRiderFlag",1,"Go")
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
THUN H 0 A_TakeInventory("TopRiderFlag",1)
THUN H 0 SetPlayerProperty(0,0,3)
THUN H 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
THUN H 1 A_Raise
Loop
Fire:
THUN H 0 A_JumpIfNoAmmo("NoAmmo")
THUN H 0 A_JumpIfInventory("TopRiderFlag",1,"NoAmmo")
THUN H 0 A_PlaySoundEx("weapon/mbuster","Weapon")
THUN H 0 A_SpawnItemEx("TopRider", 128, 0, 1, 0, 0, 0, 0)
THUN H 0 A_TakeInventory("TopRiderAmmo",4)
THUN IJ 5
THUN H 14
THUN H 0 A_Refire
Goto Ready+1
Go:
THUN H 0 A_JumpIfNoAmmo("NoAmmo")
THUN H 0 SetPlayerProperty(0,1,3)
THUN H 0 A_ChangeVelocity(momx*1.5, momy*1.5, momz, CVF_REPLACE)
THUN H 5
THUN H 0 A_TakeInventory("TopRiderAmmo",2)
THUN H 0 A_SpawnItemEx("TopRiderDamager")
goto Ready+1
NoAmmo:
THUN H 1 ACS_Execute(979,0)
THUN H 0 A_TakeInventory("TopRiderFlag",1)
THUN H 0 SetPlayerProperty(0,0,3)
THUN H 18
Goto Ready+1
}
}

actor TopRiderAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 56
+INVENTORY.IGNORESKILL
}

actor TopRiderDamager
{
PROJECTILE
damagetype "TopSpin"
Obituary "$%o was spun right round by $k's Top Rider"
+RIPPER
+NOINTERACTION
+DONTBLAST
damage (0)
radius 1
height 1
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_Explode(20,90,0)
stop
}
}
