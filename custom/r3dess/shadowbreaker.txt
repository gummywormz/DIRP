actor ShadowBreakerInit
{
PROJECTILE
Speed 20
Damage (10)
damagetype "ShadowBlade"
Obituary "$%o was sliced up by $k's Shadow Breaker"
+RIPPER
-NOGRAVITY
Radius 7
Height 7
scale 2.5

States
{
Spawn:
SBLA ABAB 3
loop
Death:
TNT1 A 1
TNT1 A 0 A_PlaySoundEx("weapon/shadow","Voice")
TNT1 A 1 A_SpawnItemEx("ShadowBreakerSplit",0,0,1,20,0,0,0)
TNT1 A 1 A_SpawnItemEx("ShadowBreakerSplit",0,0,1,20,0,0,90)
TNT1 A 1 A_SpawnItemEx("ShadowBreakerSplit",0,0,1,20,0,0,180)
TNT1 A 1 A_SpawnItemEx("ShadowBreakerSplit",0,0,1,20,0,0,270)
stop
}
}

actor ShadowBreakerSplit
{
PROJECTILE
Speed 30
Damage (7)
damagetype "ShadowBlade"
Obituary "$%o was sliced up by $k's Shadow Breaker"
+RIPPER
Radius 7
Height 7
scale 2.5

States
{
Spawn:
SBLA ABAB 3
loop
Death:
TNT1 A 1
stop
}
}

actor ShadowBreakerWep : BaseMM8BDMWep
{
Weapon.AmmoUse 1
Weapon.AmmoGive 28
Weapon.SlotNumber 3
Inventory.Pickupmessage "Power Up! Shadow Breaker"
Obituary "$%o was sliced up by $k's Shadow Breaker"
Tag "Shadow Breaker"
weapon.ammotype "ShadowBreakerAmmo"
inventory.icon "SHBKICO"
States
{
Spawn:
SHBK A -1
STOP
Ready:
BUSH A 0 ACS_ExecuteAlways(998,0,4)
BUSH A 0 A_JumpIfNoAmmo("ReadyNoAmmo")
BUSH A 1 A_WeaponReady
Goto Ready+2
ReadyNoAmmo:
BUS2 D 1 A_WeaponReady
BUS2 D 0 A_JumpIfNoAmmo("ReadyNoAmmo")
Goto Ready+1
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
BUSH A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
BUSH A 1 A_Raise
Loop
Fire:
BUSH A 0 A_JumpIfNoAmmo("NoAmmo")
BUSH A 0 A_PlaySoundEx("weapon/shadow","Weapon")
BUSH A 0 A_FireCustomMissile("ShadowBreakerInit",0,1,8,0)
BUSH BCDEFG 2
TNT1 A 4
BUSH HI 1
BUSH A 2
BUSH A 0 A_Refire
Goto Ready+1
NoAmmo:
BUS2 D 1 ACS_Execute(979,0)
Goto ReadyNoAmmo
}
}

actor ShadowBreakerAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}