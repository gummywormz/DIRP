actor ConcussiveKnuckle
{
PROJECTILE
scale 2.5
damagetype "HardKnuckle"
Obituary "%o got a headache from %k's concussive knuckle"
Speed 40
Radius 12
Height 10
Damage (30)
States
{
Spawn:
HARD A 12
HARD A 1 A_SpawnItemEx("CKExploder")
loop
Death:
MMFX BCDE 2
stop
}
}


actor CKExploder 
{
PROJECTILE
+RIPPER
+NOGRAVITY
Height 1
Radius 1
Speed 0
Damage (0)
Scale 2.5
States
{
Spawn:
MMFX B 1
MMFX B 35
goto XDeath
XDeath:
CRAS A 0 A_PlaySound("weapon/crashexplode")
CRAS K 3 A_Explode(18,64,0)
CRAS LM  3
CRAS N 3 A_Explode(18,64,0)
CRAS OP 3
CRAS Q 3 A_Explode(18,64,0)
CRAS RS 3
CRAS T 3 A_Explode(18,64,0)
CRAS VW 3
stop
}
}

actor ConcussiveKnuckleWep : BaseMM8BDMWep
{
//$Category MM8BDM-Weapons
//$Title Hard Knuckle
Weapon.AmmoUse 10
Weapon.AmmoGive 30
Weapon.SlotNumber 5
Inventory.Pickupmessage "Power Up! Concussive Knuckle!"
Obituary "%o got a headache from %k's concussive knuckle"
Tag "Concussive Knuckle"
weapon.ammotype "ConcussiveKnuckleAmmo"
inventory.icon "CNKNICO"
States
{
Spawn:
CNKN A -1
STOP
Ready:
FIST Q 0 ACS_ExecuteAlways(998,0,18)
FIST Q 1 A_WeaponReady
Goto Ready+1
Deselect:
TNT1 QQQQQQQQQQQQQQQQQQQQQQ 0 A_Lower
FIST Q 1 A_Lower
Loop
Select:
TNT1 QQQQQQQQQQQQQQQQQQQQQQ 0 A_Raise
FIST Q 1 A_Raise
Loop
Fire:
FIST Q 0 A_JumpIfNoAmmo("NoAmmo")
FIST Q 0 A_PlaySoundEx("weapon/mbuster","Weapon")
FIST B 2 A_SpawnItemEx("HardKnuckleFX",-1,8,32,0,0,0)
FIST CDE 2 
FIST B 0 SetPlayerProperty(0,1,0)
FIST F 2 A_FireCustomMissile("ConcussiveKnuckle",0,1,10,-2)
//FIST F 0 A_ChangeVelocity(-Cos(pitch)*9, 0, sin(pitch)*5,CVF_RELATIVE)
FIST F 0 A_ChangeVelocity(-9, 0, 0, CVF_RELATIVE)
FIST GHI 2
FIST B 0 SetPlayerProperty(0,0,0)
FIST JKLMNOP 2
FIST Q 0 A_Refire
Goto Ready+1
NoAmmo:
FIST Q 1 ACS_Execute(979,0)
Goto Ready+1
}
}

actor ConcussiveKnuckleAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 30
+INVENTORY.IGNORESKILL
}
